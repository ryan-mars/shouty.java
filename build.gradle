plugins {
    id 'java'
  	id 'eclipse' // optional (to generate Eclipse project files)
	id 'idea' // optional (to generate IntelliJ IDEA project files)
}

group = 'cucumber-ltd'
version = '0.0.1'
description = 'Shouty'
java.sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    // mavenLocal()
    // maven {
    //     url = uri('https://repo.maven.apache.org/maven2/')
    // }
}

dependencies {
    implementation 'org.springframework:spring-context:5.3.18'
    testImplementation 'org.springframework:spring-test:5.3.18'

    implementation 'io.cucumber:cucumber-spring:7.2.0'
    testImplementation 'io.cucumber:cucumber-java:7.2.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.2.0'

    testImplementation 'org.junit.platform:junit-platform-suite:1.8.2'
    testImplementation(platform('org.junit:junit-bom:5.8.2'))
	testImplementation('org.junit.jupiter:junit-jupiter')
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                '--plugin', 'pretty', 
                '--plugin', 'html:target/cucumber-report.html', 
                '--glue', 'shouty', 
                'src/test/resources']
        }
    }
}

test {
    useJUnitPlatform()
    systemProperty("cucumber.junit-platform.naming-strategy", "long")
     testLogging {
         events "passed", "skipped", "failed"
         exceptionFormat "full"
         showStandardStreams true
         showExceptions true
         showStackTraces true
     }
     outputs.upToDateWhen { false }
}